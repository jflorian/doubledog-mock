# modules/mock/manifests/target/repo.pp
#
# == Define: mock::target::repo
#
# Defines a package repository to be made available for a mock build target.
#
# === Parameters
#
# ==== Required
#
# [*namevar*]
#   An arbitrary identifier for the repo instance unless the "reponame"
#   parameter is not set in which case this must provide the value normally
#   set with the "reponame" parameter.
#
# [*base_arch*]
#   The base architecture for the build target.
#
# [*family*]
#   The build target's distribution family.  E.g., 'fedora' or 'epel'.
#
# [*release*]
#   The build target's distribution release.  E.g., '20'.
#
# ==== Optional
#
# [*baseurl*]
#   A URL or list of URLs to where the repository's `repodata' directory can
#   be found.  Each can be any of: http://, ftp:// or file://.  See
#   "failovermethod" for how to stipulate which should be used and when.
#
# [*cost*]
#   Integer indicating the relative cost of accessing this repository.  This
#   is useful for weighing one repo's packages as greater/less than any other.
#   The default is 1000.
#
# [*enabled*]
#   A Boolean that indicates whether this package repository is to be used or
#   not.  The default is true.
#
# [*failovermethod*]
#   Either `roundrobin' ( the default) or `priority'.  `roundrobin'  randomly
#   selects a URL out of the list of URLs to start with and proceeds through
#   each of them as it encounters a failure contacting the host.  `priority'
#   starts from the first baseurl listed and reads through them sequentially.
#
# [*gpgcheck*]
#   A Boolean that indicates whether to perform a GPG signature check on
#   packages from this repository.  The default is false.
#
# [*gpgkey*]
#   A URL or list of URLs (like "baseurl") pointing to the ASCII-armored GPG
#   key file for the repository.
#
# [*metalink*]
#   Specifies a URL to a metalink file for the repomd.xml.  A list of mirrors
#   for the entire repository are generated by converting the mirrors for the
#   repomd.xml file to a baseurl.
#
# [*mirrorlist*]
#   Specifies a URL to a file containing a list of baseurls.  This can be used
#   instead of or with the "baseurl" parameter.
#
# [*reponame*]
#   A human readable string describing the repository.  This may be used in
#   place of "namevar" if it's beneficial to give namevar an arbitrary value.
#
# === Authors
#
#   John Florian <jflorian@doubledog.org>
#
# === Copyright
#
# Copyright 2016-2017 John Florian


define mock::target::repo (
        Enum['i386', 'x86_64'] $base_arch,
        String[1] $family,
        String[1] $release,
        Optional[String[1]] $baseurl=undef,
        Integer[0] $cost=1000,
        Boolean $enabled=true,
        Enum['priority', 'roundrobin'] $failovermethod='roundrobin',
        Boolean $gpgcheck=false,
        Optional[String[1]] $gpgkey=undef,
        Optional[String[1]] $metalink=undef,
        Optional[String[1]] $mirrorlist=undef,
        String[1] $reponame=$title,
    ) {

    if $baseurl == undef and $metalink == undef and $mirrorlist == undef {
        fail("one of 'baseurl', 'metalink' or 'mirrorlist' must be set")
    }

    if $gpgcheck and $gpgkey == undef {
        fail("'gpgkey' must be set when gpgcheck is true")
    }

    ::concat::fragment { "mock-${family}-${release}-${base_arch} ${title}":
        target  => "/etc/mock/${family}-${release}-${base_arch}.cfg",
        order   => '200',
        content => template('mock/repo.erb'),
    }

}
